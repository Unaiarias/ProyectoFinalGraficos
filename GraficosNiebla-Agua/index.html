<!DOCTYPE html>
<html lang="es">
<head>
    <meta charset="utf-8">
    <title>Phong + Niebla + Agua (WebGL2)</title>

    <style>
        body {
            background: #222;
            color: white;
            font-family: sans-serif;
        }

        canvas {
            border: 1px solid white;
            width: 600px;
            height: 600px;
        }
    </style>

    <!-- ================= VERTEX SHADER ================= -->
    <script id="vs" type="text/plain">
        #version 300 es
        precision mediump float;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform float uTime;     // tiempo (niebla + agua)
        uniform int isWater;     // 0 = cubo | 1 = agua

        in vec3 VertexPosition;
        in vec3 VertexNormal;

        out vec3 ecPositionOut;
        out vec3 ecNormalOut;

        void main() {

            // ===== POSICIÃ“N BASE (CUBO Y AGUA) =====
            vec3 pos = VertexPosition;
            vec3 normal = VertexNormal;

            // ===== AGUA (DISPLACEMENT MAPPING) =====
            if (isWater == 1) {
                float w1 = sin(pos.x * 2.5 + uTime * 1.5);
                float w2 = cos(pos.z * 2.0 + uTime * 1.2);
                float w3 = sin((pos.x + pos.z) * 1.5 + uTime);

                float height = (w1 + w2 + w3) * 0.08;
                pos.y += height;

                normal = normalize(vec3(
                    -cos(pos.x * 2.5 + uTime * 1.5),
                     1.0,
                    -sin(pos.z * 2.0 + uTime * 1.2)
                ));
            }

            vec4 ecPosition = modelViewMatrix * vec4(pos, 1.0);
            ecPositionOut = vec3(ecPosition);
            ecNormalOut = normalize(normalMatrix * normal);

            gl_Position = projectionMatrix * ecPosition;
        }
    </script>

    <!-- ================= FRAGMENT SHADER ================= -->
    <script id="fs" type="text/plain">
        #version 300 es
        precision mediump float;

        in vec3 ecPositionOut;
        in vec3 ecNormalOut;
        out vec4 fragmentColor;

        struct LightData {
            vec3 Position;
            vec3 La;
            vec3 Ld;
            vec3 Ls;
        };
        uniform LightData Light;

        struct MaterialData {
            vec3 Ka;
            vec3 Kd;
            vec3 Ks;
            float alpha;
        };
        uniform MaterialData Material;

        /* ================= NIEBLA ================= */
        uniform float fogMin;
        uniform float fogMax;
        uniform vec3 fogColor;
        uniform int fogEnabled;
        uniform int fogMode;
        uniform float uTime;

        vec3 phong(vec3 N, vec3 L, vec3 V) {
            vec3 ambient = Material.Ka * Light.La;
            float NdotL = max(dot(N, L), 0.0);
            vec3 diffuse = NdotL * (Light.Ld * Material.Kd);
            vec3 specular = vec3(0.0);

            if (NdotL > 0.0) {
                vec3 H = normalize(L + V);
                float NdotH = max(dot(N, H), 0.0);
                specular = pow(NdotH, Material.alpha) * (Light.Ls * Material.Ks);
            }
            return ambient + diffuse + specular;
        }

        void main() {
            vec3 N = normalize(ecNormalOut);
            vec3 V = normalize(-ecPositionOut);
            vec3 L = normalize(Light.Position - ecPositionOut);

            vec3 litColor = phong(N, L, V);

            if (fogEnabled == 0) {
                fragmentColor = vec4(litColor, 1.0);
                return;
            }

            float dist = abs(ecPositionOut.z);

            float drift = ecPositionOut.x * 1.4 + ecPositionOut.y * 0.8 + uTime * 1.5;
            float bank = 0.5 + 0.5 * sin(drift);
            bank = pow(bank, 2.5);
            dist += bank * 2.5;

            float fogFactor;
            if (fogMode == 0) {
                fogFactor = (fogMax - dist) / (fogMax - fogMin);
            } else {
                float dnorm = clamp((dist - fogMin) / (fogMax - fogMin), 0.0, 1.0);
                fogFactor = exp(-pow(dnorm * 3.0, 2.0));
            }

            fogFactor = clamp(fogFactor, 0.0, 1.0);
            vec3 finalColor = mix(fogColor, litColor, fogFactor);
            fragmentColor = vec4(finalColor, 1.0);
        }
    </script>

</head>

<body>
    <h2>Phong + Niebla + Agua</h2>
    <canvas id="glCanvas" width="600" height="600"></canvas>

    <script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.4.0/gl-matrix.js"></script>
    <script src="app.js"></script>
</body>
</html>
